:gmmainmenu
invkill
perkill
numbday=0
hide_use_Записка_Перевернуть=1
inv_visible false

html true
<html><br>
pln <center><B>Как я стал пиратом.</B></center><br>

style_dos_textcolor= 1

pln Небольшой бар провинциального города, за барной стойкой одиноко сидит молодой человек.<br>
btn prolog1, Начать рассказ
btn author, Автор
</html>
end


:author
<html><br>
style_dos_textcolor=0
pln Автор этого безобразия - Shushkart.
btn gmmainmenu, Назад
</html>
end

:prolog1
html false

pln - Итак, сижу я в баре, пью пиво. И тут заходит какой-то старик, медленно подходит к стойке бара и заказывает два виски, а потом садится на стул рядом со мной.
pln Молодой человек молча налил себе еще пива и продолжил слушать рассказ.
pln - Незнакомец протянул мне один стакан и драматично произнес... 
pln Откашлявшись, я немного изменил голос, чтобы быть более похожим на того старика: "Я должен рассказать тебе свою историю, чтобы с тобой такого не приключилось!"
pln Молодой человек отложив свое пиво слушал меня, кажется, я смог его заинтересовать.
btn prolog2, - Валяй, - сказал я старику.
end

:use_Бутылка "Одноногий Джо"_Перелить в пустую бутылку
pln Настоящее кощунство выдавать этот разбавленный уксус за приличную выпивку, но выбора не было. Откупорив бутылку я почувствовал резких запах режущий глаза, пришлось переливать жидкость зажмурившись и сдерживая слезы, но, тем не менее, я не пролил ни капли.
inv- Бутылка "Одноногий Джо"
inv- Пустая бутылка "Якорь в глотке"
inv+ Виски "Якорь в глотке"
didpoilo=1
end

:use_Карта_Осмотреть
pln Небольшой клочок кожи на котором нарисована карта какого-то острова в море, помеченного красным крестом. И почему я решил что там можно найти сокровища? Но отступать было уже некуда...
end

:use_Дневник_Осмотреть
pln Пролистав дневник до последней записи я прочел следующее: "Вспомнить что я делаю на пристани". Вряд ли это мне чем-то поможет.
end

:use_Картина_Осмотреть
inv+ Записка
hide_use_Записка_Перевернуть=1
hide_use_Картина_Осмотреть=1
pln Возможно сама картина не представляла никакой художественной ценности, но ее рамка была потрясающа красива, явно ручной работы и выполнена из редких пород дерева. Мне она так понравилась что я начал осматривать ее со всех сторон, и нашел прикрепленную записку сзади картины.
end

:use_Записка_Осмотреть
hide_use_Записка_Перевернуть=0
pln Небольшой листок толстой бумаги с надписью: "Переверни листок".
end

:use_Записка_Перевернуть
sawfirsthint=1
pln На другой стороне записки тоже была надпись: "Посмотри под камнем".
end





:prolog2
style_dos_textcolor=0
pln Старик одним глотком осушил свой стакан с виски, после чего взял мой стакан и отправил его содержимое себе в желудок.
pln - Раньше я был моряком, - сделав жест бармену, начал старик.
pln Как только бармен поставил еще пару рюмок с виски, старик незамедлительно опустошил их.
pln - Много лет я ходил по морям и океанам, был счастлив и не знал никаких бед, - кивнув бармену он продолжил. - Но как-то раз я повстречал девушку и потерял голову.
pln Я хотел было взять один из стаканов с виски, но старик опередил меня, выпив сразу из обоих.
pln - Много раз я просил ее руки, но она была непреклонна, - старик подал знак бармену. - Но в конце концов она поддалась моим уговорам и согласилась, но с одним условием: "Если добудешь мне самую большую рыбу из всех которые были пойманы - я выйду за тебя!" - сказала она тогда.
pln Старик потянул свои клешни к новым рюмкам с виски, но я был уже наготове и смог ухватить одну из них, виски обожгло горло и приятно согрело внутренности.
pln - С тех пор моя жизнь превратилась в одну большую охоту, я не видел сушу годами охотясь за той рыбиной...
pln Незнакомец немного погрустнел, а бармен уже сам подливал алкоголь.
pln - И вот однажды мне улыбнулась удача, я смог поймать столь огромную рыбу, которую еще никто не видел!
pln В глазах старика блеснул огонек, и взяв стакан с виски он продолжил:
pln - Вернувшись домой, я первым делом отправился к ней, я был удивлен что она все ждала меня, - старик улыбнулся и выпил еще один стакан. - И вскоре после моего возвращения мы поженились!
btn prolog3, - И жили долго и счастливо? - предположил я.
end

:prolog3
pln - Нет, она бросила меня отсудив все мое имущество и с тех пор я - бездомный бродяга, бесцельно шатаюсь по барам и постоянно пью, - закончил старик выпив последний стакан.
pln Небольшое подозрение вместе с жалостью к рассказчику заползло в мои мысли:
pln - Но если у тебя нет денег, как ты собираешься за все это платить?
pln - Никак! - воскликнул он и с невиданной для старика ловкостью выбежал из бара.
pln Бармен грозно уставился на меня ...
btn prolog4, Далее
end

:prolog4
pln Оставив всю недавно полученную зарплату, а также пару зубов в руках вышибал из бара, я бесцельно бродил по улице размышляя о том, где бы раздобыть денег. На улице шел сильный дождь, периодически сверкала молния под громкий рокот грома, и чтобы укрыть от ливня, а также залить за воротник, я зашел в первый попавшийся бар. Пока капли дождя стекали с моей одежды на пол довольно уютного паба, я осматривался в поисках "жертвы".  Заметив у стойки бара мужчину средних лет, который он что-то пил и кажется, разговаривал сам собой, я решил что он вполне подходит. Подсев к нему я произнес:
pln - Я должен рассказать вам свою историю, чтобы с вами такого не случилось! - сделав знак бармену, начал я. - Два виски, пожалуйста.
pln - Валяй, - безразлично произнес мой новоиспеченный собутыльник.
pln Словив дежавю и залпом выпив оба стакана, я рассказал историю, которую недавно поведал мне тот старик, и уже будучи довольно "готовым" я уже было собрался свалить из бара пока бармен ничего не заподозрил, но тут, мужчина, выслушавший "мою" историю, произнес:
pln - Твой рассказ, конечно, интересен, но у меня есть история получше.
pln Опустив свою задницу обратно на стул, я решил послушать, что он скажет, в свою очередь мужчина опустошил стакан, прочистил горло и сказал:
btn prolog5, Далее
end

:prolog5
pln - Тот вечер мне никогда не забыть, солнце отработанным движением скользнуло за горизонт ...
pln Не успел он договорить первое предложение своей истории, как прогремел оглушительный гром, двери паба распахнулись, и под ослепительную вспышку внутрь вошел седовласый старик в больших темных очках и с небольшой собакой на поводке. 
pln - Это он! - вскочив со стула и схватившись за сердце, воскликнул мой новый приятель. - Он нашел меня!
pln И спустя пару секунд мужчина, словно подкошенный, свалился на пол.
pln - Вызови скорую! - крикнул я бармену склонившись над лежащим на полу бара мужчиной, - И два виски мне.
pln Тем временем слепой старик спокойно прошел и, опрокинув по пути пару стульев, сел за один из свободных столиков.
pln - It’s dangerous to go alone! Take this, - прохрипел мужчина и из последних сил достал странный сверток из внутреннего кармана пиджака.
pln Я даже не успел задуматься, почему это он заговорил на английском как послышался вой сирен скорой помощи. “Что-то быстро они”, – промелькнула мысль в моей голове...
pln Сжимая странный сверток в руках, я стоял под проливным дождем около входа в бар и молча наблюдал как моего нового знакомого увозят на машине скорой помощи. Когда оцепенение спало, я разжав кулак развернул сверток - плотный кусок материи с нанесенным на нем рисунком, это была карта...
btn prolog6, Далее
end

:prolog6
pln Следующее что я помню, это то как я стоял посреди городского порта, было около шести часов утра, воздух был пропитан солью, запахом гниющей рыбы и чем-то еще не менее отвратительным. Голова раскалывалась так словно я не спал трое суток, или пил беспробудно всю неделю, или по моей голове проехались грузовиком, нельзя было исключать ни один из вариантов.
pln “Что я тут делаю?”  - почесав затылок, попытался вспомнить я. – “Хм, даже думать больно”.
pln Бродя по порту я вспоминал события прошлой недели, все было словно в тумане, бар, мужчина, слепой старик, непонятная карта.
btn docksfalse, К пристани
btn pab, В бар
btn streets, Пройтись по улицам
end

:harbor
pln Ничем не примечательный морской порт небольшого городка, рядом был только бар и пристань с которой отходили суда.
if remember=0 then btn docksfalse, К пристани
if remember=1 then btn dockstrue, К пристани
btn pab, В бар
if remember=0 then btn streets, Пройтись по улицам
end

:docksfalse
if count_docksfalse=1 then pln Приятный морской бриз обдувал меня со всех сторон. На пристани стояло всего два корабля, сейчас они мне были совершенно не интересны.
if count_docksfalse>1 then pln Не знаю зачем я еще раз подошел к пристани, возможно мне понравился запах свежего морского воздуха.
btn harbor, Уйти с пристани
end

:streets
remember=1
inv+ Дневник
inv+ 10 долларов
inv+ Карта
inv_visible true
pln Чтобы немного освежить память я решил пройтись по улочкам рядом с пристанью, к моему большому удивлению это помогло - споткнувшись об первый попавшийся камень я упал и треснулся головой, и тут в моей голове все прояснилось. Пришел я сюда из-за той карты, что мне дал странный мужчина в баре, царство ему небесное. Пошарив в карманах я нашел не только ее, но и небольшой дневник, а также горсть мелочи. Итак, я пришел сюда чтобы нанять капитана, который бы согласился отвезти меня на остров за сокровищами, указанном на карте!
btn harbor, Уйти с пристани
end

:pab
if count_pab=1 then pln Самый обыкновенный бар, сейчас он пустовал, бармен за стойкой одиноко протирал очередной стакан.
if count_pab>1 and didpoilo=0 then pln Я зашел в бар, тут было все также пусто, только бармен стоял за стойкой и что-то бормотал.
if didpoilo=1 then pln Не знаю сколько прошло уже времени, но в баре начал собираться народ, несколько столиков были заняты выпивающими людьми.
btn pabbarmen, Подойти к бармену
if didpoilo=1 and count_pabcapnice=0 then btn pabcapnice, Подсесть к капитану корабля
if count_pabbarmenneedhire3>0 and count_pabcaphh=0 then btn pabcaphh, Подсесть к человеку в углу
btn harbor, Выйти из бара
end

:pabcaphh
inv- "Одноногий Джо"
pln За самым грязным столиком в углу бара сидел, вернее лежал на нем, и громко храпел какой-то человек в грязном пальто. У него длинные сальные волосы распластанные по всему столу, густая борода и повязка на одном глазу. Трудно определить сколько лет ему было, но вид ему можно было дать 60-70.
pln Подойдя к нему я осторожно, чтобы не испачкаться, дотронулся до его плеча:
pln - Капитан, с вами можно поговорить?
pln Не прекращая храпеть, старик открыл глаз, оценивающе осмотрел на меня с ног до головы, и снова закрыл, продолжив лежать на столе.
pln - Капитан, - повторил я, - разрешите угостить вас этим прекрасным напитком?
pln Увидев бутылку в моих руках, капитан оживился и выпрямился на стуле:
pln - Садись, - довольно произнес он после изучения этикетки.
pln - Итак, у меня есть отличное предложение...
pln Я рассказал ему весь свой план, не знаю, слушал ли он меня или нет, капитан был так увлечен выпивкой, что казалось не замечал ничего вокруг. Наконец, когда пойло в бутылке подошло ко дну, он произнес:
pln - Блестящий план! Я в восторге!
btn chapter2, - Отлично! Тогда в путь, - обрадовался я.
end

:pabcapnice
inv- Виски "Якорь в глотке"
pln Капитаном оказался молодой человек лет 35, он был одет в дорогой белый костюм, коротко подстрижен и на носу висели маленькие очки с прозрачными стеклами.
pln - Здравствуйте сэр! - осторожно начал я - Разрешите вас угостить этим отличным виски!
pln Молодой человек сделал жест рукой, позволяя сесть. Разлив виски по стаканам, я продолжил:
pln - Сэр, у меня есть отличное предложение!
pln Капитан кивнул, достав из кармана пиджака большую сигару. В свою очередь я вытянул из кармана сложенную в четверо карту и расправив ее, продолжил:
pln - Судя по этой карте, - про то что эту карту мне дал какой-то пьяный бомж, я решил умолчать, - На острове, неподалеку отсюда, спрятаны бесчисленные сокровища.
pln Капитан взял в руки карту и уставился на нее недоверчивым взглядом. Через некоторое время, он с тяжелым вздохом отложил карту, налил в стакан немного виски и сказал:
pln - Расходы на сбор команды я возьму на себя, с вас провизия, топливо, инвентарь, плюс зарплата мне и команде ...
pln Покуривая сигару и вертя в руках стакан с алкоголем, капитан изображал мыслительный процесс.
pln - Итого около 15 000, - наконец выдал он, поправив воротник.
pln - Кхм, - мой лоб покрылся потом, - у меня сейчас небольшие проблемы с деньгами и я думал что мы сможем поделить найденные сокровища ...
pln Жестом остановив меня, молодой человек холодно проговорил:
pln - Ничем не могу помочь, в кредит я не работаю, - промолвил капитан, и потянулся к стану с виски, дав понять что разговор окончен.
btn pabcapnice2, Встать из-за столика
end

:pabcapnice2
askgoodcaptain=1 
pln Не успел я отойти от столика и на пару метров, как сзади послышался сдавленный кашель и крик: "Воды, воды!". Видимо капитан все-таки попробовал мое пойло, довольно улыбнувшись я пошел дальше.
btn pab, Далее
end

:pabbarmen
pln - Чем могу помочь? - спросил бармен меня.
if remember=1 and seenships=0 and count_pabbarmenneedhire2=0 then btn pabbarmenneedhire, - Я хочу нанять корабль, не подскажете как это сделать?
if remember=1 and seenships=1 and askgoodcaptain=0 and count_pabbarmenneedhire2=0 then btn pabbarmenneedhire2, - Не подскажете где найти капитана того чудесного судна что стоит в порту?
if remember=1 and seenships=1 and askgoodcaptain=1 and coun_pabbarmenneedhire3=0 then btn pabbarmenneedhire3, - А что насчет капитана той развалюхи?
if remember=0 then btn pabbarmenwhyimhere, - Вы случайно не знаете зачем я здесь?
if tookbottle=1 and boughtpoilo=0 then btn pabbrpoilo, - Дайте мне, пожалуйста, ваше самое дешевое пойло!
btn pab, - Ничего, спасибо
end

:pabbarmenneedhire3
inv+ "Одноногий Джо"
pln Бармен взглянул на меня так, словно провожал в последний путь, смесь жалости и отчаяния читались в его глазах.
pln - Он сидит за тем столиком, - указал бармен, протягивая полную бутылку виски, - И возьмите это, подарок.
btn pab, - Благодарю, - пробормотал я.
end

:pabbrpoilo
boughtpoilo=1
inv- 10 долларов
inv+ Бутылка "Одноногий Джо"
pln Бросив презрительный взгляд, бармен достал какую-то грязную бутылку из-под стойки. Смахнув пыль с этикетки я прочитал: "Одноногий Джо, 70% чистого спирта, клянусь своим корытом, ярр!" 
pln - 10$ - протянул руку бармен.
btn pab,  - Спасибо, - отдав последние деньги сказал я.
end

:pabbarmenneedhire2
pln - Хороший выбор, это самый знаменитый капитан, этого города, куда он только не ходил, - бармен задумался на минуту. - Приходите через пару часов, он всегда сидит за тем столиком. 
pln - Спасибо, - запомнив столик поблагодарил я хозяина.
pln - Ах да, - бармен наклонился и заговорчески прошептал, - если хотите договориться с ним, то угостите его виски, которое он всегда пьет.
pln Достав из-под стойки выпивку бармен показал ее мне, ценник на ней совсем не обрадовал меня - 100$ за бутылку. 
pln - Благодарю.
pln Теперь оставалось только дождаться обеда и как-то раздобыть то дорогущее пойло.
btn harbor, Выйти из бара
end

:pabbarmenwhyimhere
pln - Хм, - бармен почесал подбородок - чтобы выпить?
btn pabbarmenwhyimhere2, - Точно! Мне пива, пожалуйста.
btn pab, - Нет, не за этим.
end

:pabbarmenwhyimhere2
remember=1
inv+ Дневник
inv+ 10 долларов
inv+ Карта
inv_visible true
pln С каждым глотком пива моя память прояснялась, отрывки событий прошлой недели сливались в полную картину. Я вспомнил того мужчину в баре и карту что он мне дал, покопавшись в карманах я нашел не только ее, но и пару своих вещей. Я понял что нахожусь здесь для того чтобы нанять капитана корабля, приплыть на остров и найти там сокровища!
btn pab, Далее
end

:pabbarmenneedhire
pln - Это не трудно, вам надо найти капитана и договорится с ним, обычно они собираются тут когда ничем другим не заняты. Но сейчас свободных капитанов можно по пальцам руки пересчитать, - бармен достал из-под стойки правую руку, вместо кисти на ней был железный крюк с двумя острыми концами, - большинство уже ушло в море. Сходите на пристань, посмотрите какие суда остались.
btn pab, - Хорошо спасибо
end

:dockstrue
if count_dockstrue=1 then pln Придя на пристань, я осмотрелся, хозяин бара был прав - тут было всего два корабля. Оставалось лишь выбрать нужный и найти его капитана.
if count_dockstrue>1 then pln Я вернулся на пристань, корабли стояли на своих местах.
btn docktrship, Осмотреть корабли
btn harbor, Уйти с пристани
end

:docktrship
image="goodship.jpg"
pln Первый корабль выглядел весьма неплохо, отправиться в путь на таком было бы большой удачей.
btn docktrshipnextship, Следующее судно
end

:docktrshipnextship
seenships=1
image="badship.jpg"
pln Второй корабль выглядел намного хуже, и это еще мягко сказано. Днище было продырявлено в нескольких местах, а палуба вся в грязи и каких-то пятнах. На земле рядом с судном лежала большая куча мусора.
if count_pabbarmenneedhire2=1 and count_docktrshipnextshipgarbage=0 then btn docktrshipnextshipgarbage, Покопаться в мусоре
btn harbor, Уйти с пристани
end

:docktrshipnextshipgarbage
tookbottle=1
inv+ Пустая бутылка "Якорь в глотке"
pln Покопавшись в мусоре я нашел пустую бутылку из-под элитного виски "Якорь в глотке".
goto docktrshipnextship
end

:chapter2
pln - Да, только тут есть одна проблема, - сказал капитан, отхлебнув с горла, добив содержимое бутылки, - судно уже месяц как не на ходу. Двигатель сломался, и его уже не починить, но вот найти новую деталь к нему вполне возможно.
pln - И где его раздобыть?, - отчаявшись спросил я.
pln - Он стоит огромных денег, но по чистой случайности я недавно узнал что у того новенького корабля в порту точно такой же двигатель какой был и на моем судне. 
pln Сказав это капитан подмигнул и с громко поставил пустую бутылку на стол.
btn chp2yl, - И что же это за сломанная деталь?
end

:chp2yl
pln Запомнив объяснения про недостающую деталь двигателя, я вышел из бара и заметил что на улице уже успела наступить глубокая ночь. Луна светила сквозь облака едва освещая все вокруг. Добравшись до пристани, я увидел свет в будке охранника, "Он-то мне и нужен" - обрадовался я. Внутри сидел мужчина перед телевизором с бутылкой водки в руках.
pln - Выпьешь со мной? - сходу заявил тот.
pln - Э ... да? - несколько неуверенно ответил я.
pln Мы пили несколько часов, у охранника оказался неплохой запас паленой водки в будке. Вернее пил он, а я лишь сливал свою рюмку в цветок стоявший на полу, и дожидался, когда охранник наконец-то напьется в усмерть чтобы я смог проникнуть на корабль. Спустя некоторые время, пока он разливал очередную порцию в стаканы, я осторожно начал расспрашивать его:
pln - Допустим, если какой-нибудь злоумышленник захочет проникнуть на лодку …
pln - Ха! Ему надо пройти не только мимо моего зоркого взгляда, ик! - посмотрев ему в глаза, я понял что в данный момент мимо него незамеченным пройдет даже слон, обвешанный яркими гирляндами. - Но также на современных суднах установлена сигнализация, как например на том.
pln - Да, неплохая защита ...
pln - Неплохая? Да если бы не я, все эти кораблики давно бы украли! - важно сказал охранник. - Достаточно перерезать красный провод, и нет этой сигнализации, например вот этими кусачками!
pln Запомнив где лежат кусачки, я поднял стакан с водкой и произнес тост:
pln - Как говорил мой дед: "я твой дед"
pln - Умный у тебя дед, - согласился охранник.
pln Вскоре охранник громко захрапел на столе, а я прихватив кусачки вышел из будки.
btn chp2lod, Подойти к кораблю
end

:chp2lod
pln В потемках я осторожно добрался до корабля, панель сигнализации мигала зеленой лампочкой.
btn chp2lodsig, Взломать сигнализацию
end

:chp2lodsig
image="syst.jpg"
pln Достав из кармана кусачки я принялся за дело. Осторожно подцепив пластмассовую крышку, я без особого труда снял ее и поставил на землю. То, что было внутри, заставило меня сильно удивиться, а мой глаз задергаться. Все провода были красными, ну или почти красными, какой из них надо перерезать было совершенно не понятно.
btn chp2lodsig2, Перерезать красный провод
btn chp2lodsig2, Перерезать красный провод
btn chp2lodsig2, Перерезать красный провод
end

************************************************************************************************************************************************************

:chp2lodsig2
pln Закрыв глаза я перерезал красный провод, на мое счастье сигнализация все-таки отключилась, и следуя инструкциям капитана я благополучно снял необходимую железяку с двигателя судна. Когда я уходил с пристани, уже светало, зайдя в бар я нашел капитана на том же месте что и вчера. Он спал лежа на столике в обнимку с бутылкой.
pln - Капитан Артур, на нас напали, - разбудил я спящего старика.
pln После этого я кратко описал события прошлой ночи, и сказал что нам надо незамедлительно отправляться в путь. Капитан не заставил себя ждать, и вскоре мы уже были на борту корабля. Стоя на палубе и наблюдая за отдаляющимся берегом, я всей своей душой чувствовал, что долгие недели открытого моря и незабываемые приключения ожидали меня!
pln Но не прошло и получаса как капитан подошел ко мне и радостно сообщил:
btn chp3, Далее
end

:chp3
pln - Мы на месте, сынок!
pln - Как? Мы только что вышли в море, - удивился я.
pln Капитан достал карту и ткнул туда пальцем:
pln - Это остров рядом с берегом, видишь, тут даже наш бар указан!
pln Вглядевшись я понял что он прав, очертания берега на карте были знакомы. Мне ничего больше не оставалось, как попросить капитана сбросить трап на сушу и пойти исследовать остров.
btn chp3island, Сойти на берег
end

:chp3island
if count_chp3island=1 then goto chp3isfake 
pln Стоя на берегу острова я осматривался по сторонам, позади меня был корабль, слева несколько небольших деревьев, а впереди тропинка ведущая куда-то вверх.
btn chp3ship, Вернуться на корабль
btn chp3istrees, Подойти к деревьям
if count_chp3way=0 and helpdcapitan=1 and capisdead=0 then btn chp3way, Идти по тропинке
if helpdcapitan=0 and capisdead=0 then btn chp3wayfake, Идти по тропинке
if count_chp3way>0 and helpdcapitan=1 and capisdead=0 then btn chp3way, В пещеру
end

:chp3wayfake
pln Я не смог просто так уйти и бросить капитана, надо было помочь ему.
btn chp3island, Назад
end

:chp3isfake 
pln Первым спустившись по трапу, я шагнул на мокрый песок острова. В отличие от города здесь воздух был чистым и "вкусным", вдохнув его полной грудью я внезапно услышал крик боли раздавшийся сзади. Быстро обернувшись, я увидел лежащего на трапе капитана.
pln - Я споткнулся и ... а! - кэп схватился за ногу.
pln Подбежав к нему я понял что дела плохи, нога была сломана, он лежал и корчился от боли. Я попытался вспомнить все школьные уроки ОБЖ, чтобы понять что надо делать, но не смог, так как в основном я их прогуливал.
pln - Шину! - увидев мое задумчивое лицо прокричал капитан. - Найди из чего можно сделать шину!
pln - Срань господня, точно, шина! Сейчас, капитан, держитесь.
btn chp3ship, Далее
end

:chp3ship
if count_chp3ship=1 then pln Осмотрев корабль ничего похожего на шину я не нашел, и решил сделать из подручных материалов, оставалось лишь найти пару палок.
if count_chp3ship>1 then pln Я стоял на палубе корабля. 
if haelpdcap=0 and capisdead=0 and count_chp3ship>1 then p Рядом со мной лежал капитан корчась от боли и держась за ногу.
if haelpdcap=1 and capisdead=0 then p Рядом со мной лежал капитан тихо попивая ром из горла бутылки.
/*
//if haelpdcap=1 and capisdead=0 then pln Надо помочь капитану: найти обезболивающее, пару палок для шины, бинт 
*/
if capisdead=1 then p Рядом со мной неподвижно лежал капитан.
btn chp3shipcap, Подойти к капитану
btn chp3shiprub, В рубку управления
btn chp3island, Сойти с корабля
end

:chp3shipcap
if haelpdcap=0 then pln Бедняга лежал на полу корабля корчась от боли и проклинал все что попадалось ему на глаза.
if haelpdcap=1 and capisdead=0 then pln После того как я принес ему ром и наложил шину, капитану заметно полегчало, он что-то тихо бормотал попивая ром.
if capisdead=1 then pln Звеня монетами и опрокидывая пустые бутылки, разбросанные по полу, я подбежал к капитану. К сожалению я не успел, весь алкоголь кончился. Тело капитана лежало без движения среди бутылок, одна из них все еще лежала в его руке. В его крови больше не было рома, сердце не билось, он был бледен и холоден. Моя рука потянулась к голове, чтобы снять шляпу, но я вовремя вспомнил что у меня ее нет. Убедившись что я ничем не смогу ему больше помочь, я решил что пора отправляться назад.

btn chp3ship, Отойти от капитана
if capisdead=1 then btn ch4waytohome, Плыть домой
if haelpdcap=0 and count_chp3istrees2=0 then btn chp3shipcaphelp, Помочь капитану
if count_chp3istrees2=1 and count_chp3shipwitrum=0 then btn chp3shipwiorum, Помочь капитану
end

:chp3shipcaphelp
pln Нога капитана была все еще сломана, ничего не изменилось, надо было соорудить шину.
btn chp3ship, Назад
end

:chp3shiprub
if tookrum=0 and count_chp3shiprub=1 then pln Войдя в рубку я обнаружил ящик рома стоящий около штурвала, а также несколько больших досок в углу, для шины они не подошли бы, слишком длинные и толстые.
if tookrum=0 and count_chp3shiprub>1 then pln Войдя в рубку я обнаружил ящик рома стоящий около штурвала, а также несколько больших досок в углу
if tookrum=1 and count_chp3shwood=0 then pln В рубке все еще лежало несколько досок.
if tookrum=1 and count_chp3shwood=1 then pln Я снова вернулся в рубку управления и встал рядом со штурвалом.
if needwood=1 and count_chp3shwood=0 then btn chp3shwood, Взять пару досок
btn chp3ship, Назад
end

:chp3shwood
inv+ Доски
tookwood=1
pln Подойдя к куче досок я взял пару и взгромоздил их себе на плечо.
goto chp3shiprub
end

:chp3istrees
if count_chp3istrees=1 then pln Деревья на берегу были чертовски красивы, пышная зеленая листва, лучи солнца пробивающиеся сквозь крону, игра теней на земле. Я бы мог стоять и любоваться этим видом бесконечно, но крик боли капитана отчетливо донесся с корабля. Некоторые ветви деревьев были достаточно сухими, чтобы их можно было легко отломать и использовать в качестве шины.
if count_chp3istrees>1 then pln Вновь подойдя в деревьям я залюбовался прекрасным видом.
if count_chp3istrees2=0 then btn chp3istrees2, Наломать палок
btn chp3island, Отойти от деревьев
end

:chp3istrees2
inv+ Палки
pln Спустя некоторое время я нашел пару подходящих для шины палок.
btn chp3island, Отойти от деревьев
end

:chp3shipwiorum
pln - Я вернулся, капитан! - склонившись, я взялся за его больную ногу. - Сейчас я все сделаю.
pln - Сначала надо обезболить, дурень! - взвопил капитан. - В рубке есть немного рома, тащи его сюда!
btn chp3shipwitrum, Сходить за ромом
end

:chp3shipwitrum
haelpdcap=1
tookrum=1
helpdcapitan=1
pln Отойдя от капитана я пошел за ромом, обнаружив в рубке целый ящик я хотел было взять одну бутылку. Но, немного подумав, решил что надо тащить все что есть.
pln - Молодец, - произнес капитан, одобрительно посмотрев на ящик.
pln Достав бутылку из ящика капитан незамедлительно начал хлестать содержимое прямо из горла. Тем временем я принялся за его ногу. Когда я наконец закончил с накладыванием шины кэп успел выпить две с половиной бутылки.
pln - Поторопись, сынок - вытерев рот, проговорил старик, - выпивка скоро кончится, долго я не протяну.
pln - Я вернусь вовремя, с вами ничего не случится, - сжав руку капитану пообещал я. - Но просто на всякий случай: как управлять кораблем?
pln Вздохнув он объяснил мне основы управления судном, а после напутственно сказал:
pln - Теперь ты настоящий морской волк, понял меня, сынок?
pln - Яррр, – кивнул я.
btn chp3ship, Отойти от капитана
end

















************************************************************************************************************************************************************

:chp3way
if count_chp3way=1 then pln Подъем по тропе занял около получаса, она обрывалась около входа в пещеру. Решив, что отступать уже поздно я вошел в нее и прошел еще несколько метров прежде чем дошел до развилки. Слева и справа были еще два хода, а в центре к стене прилегал большой камень.
if count_chp3way>1 then pln Снова вернувшись к развилке в центре пещеры, я остановился у большого камня.

if knowway=0 then btn chp3cavefake, Пройти в глубь пещеры
btn chp3cavestone, Осмотреть камень
if sawfirsthint=2 then btn c3cvsthint2, Найти маленький камень слева
if sawfirsthint=3 then btn c3cvsthint3, Осмотреть выход
if sawfirsthint=4 then btn c3cvsttreasure1, Пройти по карте
btn chp3island, Вернуться на берег
end

:chp3cavefake
a = rnd10
if a<6 then pln Решив попытать удачу я свернул налево и около часа плутал по разным ходам пещеры, как оказалось она была не хуже лабиринта. Но к счастью дорогу назад я запомнил и благополучно вернулся.
if a>5 then pln Решив попытать удачу я свернул направо и около часа плутал по разным ходам пещеры, как оказалось она была не хуже лабиринта. Но к счастью дорогу назад я запомнил и благополучно вернулся.
goto chp3way
end

:chp3cavestone
if count_chp3cavestone=1 then inv+ Картина
if count_chp3cavestone=1 then pln Большой камень, больше похожий на какой-то алтарь, стоял около стены, над ним висела старая картина (да, я тоже тогда очень удивился тому, что она делает в пещере) судя по выцветшей краске, она находилась тут уже очень давно. Я решил взять ее с собой, для того чтобы хотя бы продать ее, если сокровища я так и не найду. И каково же было мое удивление, когда за картиной я обнаружил железную дверцу сейфа, вмонтированного в камень.
if count_chp3cavestone>1 then pln Я вновь подошел к большому камню в пещере, сейф сверху никуда не делся.
if count_ch3safe3=0 then btn ch3safe, Открыть сейф
if sawfirsthint=1 then btn c3cvsthint1, Подсмотреть под камнем
btn chp3way, Отойти от сейфа
end

:ch3safe
if knowcode=1 then goto ch3safe3
pln Сейф был закрыт, похоже кодовая панель на нем была не просто так.
btn chp3cavestone, Назад
end

:ch3safe3
capisdead=1
pln Руки тряслись от волнения и страха, что внутри будет очередная бумажка. Но, все-таки совладав с тремором я набрал код, и дверца сейфа громко щелкнула. Едва держась на ногах я потянулся к металлической ручке и открыл сейф. Знаешь, как показывают в фильмах, когда открывают сундук с сокровищами? Яркий свет, музыка, все дела? Так вот тут все было то же самое, казалось, свет сам лился из сейфа, а музыка играла внутри моей головы. Внутри сейфа лежала куча старых, но все еще в хорошем состоянии, золотых монет. Не помня себе от счастья я сгребал, их роняя на пол, и рассовывал по карманам. Наконец, когда все монеты, с пола в том числе, перекочевали в мою куртку и штаны, я вспомнил о том что капитан все еще нуждается в срочной медицинской помощи и я поспешил к нему.
btn chp3island, Вернуться на корабль
end

:c3cvsthint1
sawfirsthint=2
pln Снизу на камне была высечена надпись: "Посмотри под маленьким камнем слева".
btn chp3cavestone, Назад
end

:c3cvsthint2
sawfirsthint=3
pln Под камнем лежала небольшая записка: "А теперь над входом в пещеру".
btn chp3way, Назад
end









:c3cvsthint3
sawfirsthint=4
knowway=1
pln Над входом в пещеру была криво выцарапана маленькая надпись: "Ты сюда сокровища пришел искать или башкой крутить?". Покрутив головой, и опустив взгляд немного ниже я обнаружил карту пещеры, на который были указан необходимый мне проход, о чем свидетельствовала отметка в виде жирного креста.
btn chp3way, Назад
end

:c3cvsttreasure1
needwood=1
if count_c3cvsttreasure1=1 then pln Пройдя несколько метров по карте я дошел до небольшой пропасти, на другой стороне на земле стоял железный сундук. Подойдя к самому краю, я заметил неоконченную надпись на стене: "Pride comes befo..." дальше недописанные буквы уходили вниз и превращались в кривые линии. Видимо писавший это был менее осторожен и теперь покоился на дне. Печальный опыт предыдущего искателя приключений отбил все желание попробовать перепрыгнуть пропасть, и я решил построить небольшой мост. Оставалось только найти пару крепких деревяшек.
if count_c3cvsttreasure1>1 then pln Вновь вернувшись к пропасти я встал у края, надпись служила напоминанием что не стоит делать никаких глупостей.
if tookwood=1 and knowcode=0 then btn c3cvsttreasure2, Положить пару досок
if knowcode=1 then btn c3cvsttreasure2, Перейти на другую сторону
btn chp3way, Вернуться назад
end

:c3cvsttreasure2
knowcode=1
if count_c3cvsttreasure2=1 then pln Осторожно ступив на трещащие подо мной доски, медленно шагая я перебрался на другую сторону. Добравшись до сундука, я остановился в трепетном ожидании, все о чем я мог думать это огромная гора блестящих монеток или куча долларовых купюр на худой конец. Но меня постигло большой разочарование - после того как я наконец-то собрался с духом и приоткрыл крышку сундука, все что я обнаружил в нем это лишь клочок бумаги на котором были написаны цифры: 4 8 15 16 23 42.
if count_c3cvsttreasure2>1 then pln Еще раз перейдя опасную пропасть, в которой уже погиб как минимум один человек, по шаткому самодельному мосту, я вновь удостоверился что в сундуке все еще пусто. Да, оно того стоило.
btn chp3way, Назад в пещеру
end

:ch4waytohome
pln Заведя мотор я встал за штурвал, все было готово к отплытию - тело капитана я захоронил на берегу, так как лопаты у меня не было пришлось просто обставить его стеклянными бутылками, благо их было очень много. Поворачивая штурвал и дергая рычаги я отчалил от берега, получилось у меня это не с первого раза конечно, но для того кто никогда в жизни не управлял кораблем я был довольно неплох. В мыслях я был уже дома - купался в лучах славы и богатства, и естественно горько сожалел о смерти капитана, запивая утрату самым дорогим вином.
pln Но к сожалению на этом мои приключения не закончились. Не успел я пройти и половину пути до дома как мне на хвост сели пираты. Да, пираты, Веселый Роджер на их мачте гордо развивался по ветру. Их судно было быстроходнее и куда новее моего, поэтому им даже не пришлось стараться, чтобы догнать меня, и вскоре мой корабль был взят на абордаж. Перебравшись ко мне на борт они обыскали все каюты, но не нашли ничего кроме грязи и мусора. Пока шел обыск, я тихо стоял в углу, стараясь не звенеть монетами, но от поднятой пыли запершило в глотке, и предательский чих подкрался незаметно.
pln Спустя полчаса я стоял на борту корабля печально наблюдая как пиратское судно с моими сокровищами быстро отдаляется от меня. Наконец, собравшись с силами, я вернулся к штурвалу и продолжил путь. Вернувшись домой я поклялся найти тех мерзавцев...
btn ch4waytohome2, Далее
end

:ch4waytohome2
style_dos_textcolor= 1
pln - Вот так я и стал пиратом, - произнес я, опустошив последний стакан виски.
pln - А теперь вы выбежите из бара, оставив меня оплачивать счет? - приподняв бровь, спросил молодой человек.
pln "Черт, как он догадался, похоже, пора придумывать новую историю" - задумался я.
pln - Смотри, дракон! – крикнул я, выбросив вперед руку с отдернутым указательным пальцем. После чего вскочил со стула и со скоростью пули побежал к выходу.
end





















 